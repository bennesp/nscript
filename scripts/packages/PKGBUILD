# Maintainer: Stefano Sanfilippo (esseks)

pkgname=nscript
pkgver=1.1.2
pkgrel=1
pkgdesc='NS2 visual Network Editor'
arch=('any')
depends=('java-environment>=1.6' 'sh')
optdepends=('python: for fixnss script'
'ns2: for running simulations'
'nam: for visualizing simulation results')
makedepends=('apache-ant')
url='https://github.com/esseks/nscript/'
license=('BSD')
source=("nscript-${pkgver}.zip")
sha256sums=('05df0cb492a553218943f5a7faf531e6c4341895918bb7b8b33e5167c2911285')

package()
{
    ant clean install \
        -buildfile ${srcdir}/${pkgname}-${pkgver}/build.xml \
        -Dinstall.dir=${pkgdir} \
        -Dinstall.lib.dir=${pkgdir}/usr/share/${pkgname}/lib \
        -Dinstall.conf.dir=${pkgdir}/usr/share/${pkgname}/settings \
        -Dinstall.jar.dir=${pkgdir}/usr/share/${pkgname} \
        -Dinstall.bin.dir=${pkgdir}/usr/bin \
        -Dinstall.doc.dir=${pkgdir}/usr/share/doc/${pkgname} \
        -Dinstall.examples.dir=${pkgdir}/usr/share/doc/${pkgname}/examples

    msg "Finalizing installation..."
    # Moving license to place
    pushd ${pkgdir}/usr/share/
        mkdir -p licenses/${pkgname}
        mv doc/${pkgname}/COPYING licenses/${pkgname}/LICENSE
    popd
    # Removing suffix from executables, removing .bat
    pushd ${pkgdir}/usr/bin
        mv nscript.sh nscript
        mv fixnss.py fixnss
        rm nscript.bat
    popd
    # Correcting launch path for script
    sed -i "s|java|cd /usr/share/${pkgname} \&\& java|" \
        ${pkgdir}/usr/bin/nscript
}
